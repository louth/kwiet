buildscript {
    ext {
        // source
        kotlin_version = '1.2.31'
        jacksonKotlin_version = '2.9.5'

        // test
        junit_version = '4.12'
        hamcrest_version = '1.3'
        mockito_version = '2.15.0'

        // plugins
        bintrayPlugin_version = '1.7.3'
        coverallsPlugin_version = '2.8.2'
        consoleReporter_version = '0.5.0'
    }

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintrayPlugin_version"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:$coverallsPlugin_version"
        classpath "com.github.ksoichiro:gradle-console-reporter:$consoleReporter_version"
    }
}


apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: "com.github.kt3k.coveralls"
apply plugin: 'com.github.ksoichiro.console.reporter'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonKotlin_version"

    testCompile "junit:junit:$junit_version"
    testCompile "org.hamcrest:hamcrest-core:$hamcrest_version"
    testCompile "org.mockito:mockito-inline:$mockito_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        suppressWarnings = true
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin/'
    test.java.srcDirs += 'src/test/kotlin/'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

/**
 * Publishes to bintray
 */
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = publications = ['KwietPublication']
    publish = true
    override = true
    pkg {
        repo = 'maven'
        name = 'kwiet'
        desc = 'Kotlin voice libraries'
        userOrg = 'louth'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/louth/kwiet'
        issueTrackerUrl = 'https://github.com/louth/kwiet/issues'
        websiteUrl = 'https://louth.github.io/kwiet/'
        githubRepo = 'louth/kwiet'
        githubReleaseNotesFile = 'README.md'
        version {
            name = project.version
            released = new Date()
            gpg {
                sign = true
            }
        }
    }
}

/**
 * Create pom configuration
 */
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/louth/kwiet"
    }
}

/**
 * Create the publication with the pom configuration
 */
publishing {
    publications {
        KwietPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                pomConfig
            }
        }
    }
}

jacocoTestReport.dependsOn build